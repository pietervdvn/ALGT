
\clearpage

Properties of the syntax
-------------------------

Abstract interpretation gives us a way to lift metafunctions over parsetrees to metafunctions over sets of parsetrees. To efficiently perform computations in this abstract domain, a representation for these sets should be constructed, exploiting the underlying structure of syntaxes. In this section, we study the structure and properties of the syntax, which are used in the next section to construct a set representation for parsetrees.

Some of these properties are inherent to each syntax, others should be enforced.
For these, we present the necessary algorithms to detect these inconsistencies, both to help the programmer and to allow abstract interpretation.

### Syntactic forms as sets

When a syntactic form is declared, this is equivalent to defining a set.

The declaration of `bool ::= "True" | "False"` is equivalent to declaring $bool = \{\code{True}, \code{False} \}$. 

\begin{figure}[h!]
\center
\input{SyntaxSets.tex}
\end{figure}

This equivalence between syntactic forms and sets is the main driver for both the other properties studied and the efficient representation for sets introduced in section \ref{properties-of-the-syntax}.


### Embedded syntactic forms

Quite often, one syntactic form is defined in term of another syntactic form. This might be in a sequence (e.g. `int "+" int`) or as a bare choice (e.g. `... | int | ...`). If the latter case, each element of the choice is also embedded into the declared syntactic form.

In the following example, both `bool` and `int` are embedded into `expr`, visualized by ALGT in figure \ref{fig:nestedForms}:


	bool	::= "True" | "False"
	int	::= Number       # Number is a builtin, parsing integers
	expr	::= bool | int



This effectively establishes a _supertype_ relationship between the different syntactic forms. We can say that _every `bool` is a `expr`_, or `bool <: expr`. 



\begin{figure}[h!]
\center
\input{Supertype.tex}
\caption{Nested syntactic forms}
\label{fig:nestedForms}
\end{figure}


This supertype relationship is a lattice - the absence of left recursion (see section \ref{left-recursive-grammers}) implies that no cycles can exist in this supertype relationship.
This lattice can be visualized by ALGT, as in figure \ref{subtyping}. Note that this lattice is cached in memory, allowing a lookup for the supertype relation.


\begin{figure}[h!]
\center
\includegraphics[width=0.4\linewidth]{Subtyping.png}
\caption{A simple subtyping relationship}
\label{subtyping}
\end{figure}



### Empty sets

The use of empty strings might lead to ambiguities of the syntax. When an emtpy string can be parsed, it is unclear whether this should be included in the parsetree. Therefore, it is not allowed. 

As example, consider following syntax:

	a	::= "=" | ""
	b	::= "x" a "y" | "x" "y"
	c	::= a as

Parsing `b` over string `x y` is ambiguous: the parser might return a parstree with or without an empty token representing `a`. 
Parsing `c` is even more troublesome, here the parser might return an infinite list containing only empty `a`-elements. 

Empty syntactic forms can cause the same ambiguities, and are not allowed as well:

	a	::= 		# empty, syntax error

While a syntactic form with no choices is a syntax error within ALGT, it is possible to define an empty form through recursion:

	a	::= a

	a	::= b
	b	::= a


Note that such an empty set is, by necessity, defined by using _left recursion_. 


### Left recursive grammers

A syntactic form is recursive if it is defined in terms of itself, allowing concise definitions of arbitrary depth.
All practical programming languages do have grammers where syntactic forms are recursive. An example would be types:

	type	::= baseType "->" type | ...


Left recursion is when recursion occurs on a leftmost position in a sequence:

	a ::= ... | a "b" | ...
	

While advanced parser-algorithms, such as _LALR-parsers_ can handle this fine, it is not allowed in ALGT:

- First, this makes it easy to port a syntax created for ALGT to another parser toolchain - which possibly can't handle left recursion too.
- Second, this allows for a extremely simple parser implementation.
- Thirdly, this prevents having empty sets such as `a ::= a`.


Left recursion can be easily detected algorithmically. The algorithm itself can be found in figure \ref{fig:algoLeftRec}.
To make this algorithm more tangible, consider following syntax:

	a	::= "a" | "b" | "c" "d"
	b	::= a
	c	::= b | c "d"

First, the tail from each sequence is removed, e.g. sequence `"c" "d"` becomes `"c"`. This is expressed in lines 3-5 and has following result:

	a	::= "a" | "b" | "c"
	b	::= a
	c	::= b | d
	d	::= c

Now, all tokens, everything that is not a call to another syntactic form, is erased (lines 8-10):

	a	::= 		# empty
	b	::= a
	c	::= b | d
	d	::= c

At this point, the main loop is entered: all empty syntactic forms and their calls are deleted (lines 16 till 21 for actual deletion):


	b	::=		# empty
	c	::= b | d
	d	::= c

In the next iteration, `b` is removed as well:

	c	::= d
	d	::= c

At this point, no syntactic forms can be removed anymore. Only syntactic forms containing left recursion remain[^leftRec], for which an error message can be generated (lines 24 till 26).

[^leftRec]: Technically, syntactic forms containing a left recursive form on a leftmost position will be included too.


\begin{figure}[!h]
\begin{lstlisting}[style=algo]
# For each sequence in each syntactic form, 
# remove all but the first element
for each syntactic_form in syntax:
        for each choice in syntactic_form:
                choice.remove(1..)
 
# Remove all concrete tokens (including builtins)               
for each syntactic_form in syntax:
        for each choice in syntactic_form:
                choice.removeTokens()


empty_rules = syntax.getEmptyRules()
while empty_rules.hasElements():
        # remove each empty rule and occurences of it in other rules
        for empty_rule in empty_rules:
                syntax.remove(empty_rule)
                for each syntactic_form in syntax:
                        for each choice in syntactic_form:
                                choice.remove(empty_rule)
        empty_rules = syntax.getEmptyRules


if syntax.isEmpty():
        # all clear!
else:
        error("Left recursion detected: "+syntax)

\end{lstlisting}
\caption{The algorithm to detect left recursion in a syntax}
\label{fig:algoLeftRec}
\end{figure}


\clearpage

### Uniqueness of sequences

When a parsetree is given, pinpointing exactly which syntactic form parsed it is usefull, as this can be used to minimize the set representation later on. A syntax should thus not contain duplciate sequences.

	a ::= ... | "a" | ...
	b ::= ... | "a" | ...

	x ::= "x"
	c ::= ... | a x | ...
	d ::= ... | a x | ...

A parsetree containg `"a"` could be parsed with both `a` and `b`, which is undesired; the sequence `"a" "x"` could be parsed with both `c` and `d`.
To detect this, we compare each sequences with each every other sequence for equality.
When such duplicate sequences exist, the language designer is demanded to refactor this sequence into a new rule:

	aToken	::= "a"
	a ::= ... | aToken | ...
	b ::= ... | aToken | ...

	x ::= "x"
	ax ::= a x
	c ::= ... | ax | ...
	d ::= ... | ax | ...


This is not foolproof though. Some sequences might embed each other, as in following syntax:

	a ::= "a"
	b ::= a | "b"

	c ::= "c"
	d ::= c | "d"

	x ::= a d
	y ::= b c

Here, the string `"a c"` might be parsed with both syntactic forms `x` and `y`. There is no straightforward way to detect or refactor such a construction,
without making things overly complicated. In other words, the approach refactoring duplicate constructions can not be used. Instead, runtime annotations are used to keep track of which form originated a parsetree.

The uniqueness-constraint is still added to keep things simpler and force the language designer to write a language with as little duplication as possible. Furthermore, it helps the typechecker to work more efficient and with less errors.



