
 Language
**********

 Syntax
========

stars ::= $ "*" stars | "*"
equals ::= $ "=" equals | "="



syntaxHeader ::= "Syntax" nl equals nls

titleChar::= Upper| Lower| digit | " "
titleLine ::= $ titleChar titleLine | titleChar
title ::= nls titleLine nls stars | titleLine nls stars

commentLine ::= LineChar commentLine | LineChar
comment ::= "#" commentLine

nl ::= "\n"
nls ::= nl comment nls | nl nls | nl

rest ::= $ Any rest | Any 

### All about syntax 

identifier ::= Identifier
digit ::= Digit

wsMode ::= "::=" "$" | "::=" | "~~=" "$" | "~~="

bnfTerm ::= styleName | identifier | bnfBuiltin | String
bnfBuiltin ::= "String" | "Any" | "Upper" | "Lower" | "Digit" | "LineChar" | "Identifier"
bnfExpression ::= bnfTerm "|" bnfExpression | bnfTerm bnfExpression | bnfTerm

bnf ::= identifier wsMode bnfExpression
bnfs ::= bnf nls bnfs | bnf nls

### All about syntax styling

styleHeader ::= "Syntax Style" nl equals nls
styleName ::= "\"" identifier "\""

styleChoice ::= digit styleChoice | digit

styleLine ::= identifier "." styleChoice "->" styleName | identifier "->" styleName 
styleLines ::= styleLine nls styleLines | styleLine
 
### Main file

language ::= title nls syntaxHeader bnfs styleHeader styleLines rest  



 Syntax Style
==============

rest -> "type"
title -> "h1"
syntaxHeader -> "h2"
styleHeader -> "h2"
equals -> "noise"
identifier -> "type"
wsMode -> "operator"
bnfExpression.0 -> "constant"
bnfTerm -> "statement"
bnfBuiltin -> "keyword"
comment -> "comment"

styleLine -> "noise"
styleName -> "dirtier-hack"










